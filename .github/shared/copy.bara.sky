MAIN_URL = "git@github.com:FreeEnergyLBM/LBM.git"
MAIN_BRANCH = "shared"
MAIN_PATCH_BRANCH = "shared-patch"

SHARED_URL = "git@github.com:FreeEnergyLBM/LBM-shared.git"
SHARED_BRANCH = "main"

load("exclude", "EXCLUDE_FILES", "EXCLUDE_DIRS")
WORKFLOW_FILE = ".github/workflows/sync_shared.yml"
EXCLUDE = EXCLUDE_FILES + EXCLUDE_DIRS + [WORKFLOW_FILE]


FILTER_PATTERN = '(' + '|'.join([path.split('/')[-1] for path in EXCLUDE_FILES]) + ')'
remove_private_includes = core.replace("#include \"${x}\"${nl}", "", multiline = True, regex_groups={"x": FILTER_PATTERN, "nl":r"\n?"})
remove_private_regions = core.replace("${x}", "", multiline=True, regex_groups={"x": "(?m)^.*BEGIN-PRIVATE[\\w\\W]*?END-PRIVATE.*$\\n"}, paths=glob(["**"], exclude=[".github/shared/copy.bara.sky"]))


core.workflow(
    name = "create_patch",
    origin = git.origin(
        url = MAIN_URL,
        ref = MAIN_BRANCH,
    ),
    destination = git.destination(
        url = MAIN_URL,
        fetch = MAIN_BRANCH,
        push = MAIN_PATCH_BRANCH,
    ),
    origin_files = glob(["**"]),
    authoring = authoring.pass_thru("Copybara <copybara@example.com>"),
    mode = 'SQUASH',
    transformations = [
        remove_private_includes,
        remove_private_regions,
    ],
)


core.workflow(
    name = "push",
    origin = git.origin(
        url = MAIN_URL,
        ref = MAIN_BRANCH,
    ),
    destination = git.destination(
        url = SHARED_URL,
        push = SHARED_BRANCH,
    ),
    origin_files = glob(["**"], exclude=EXCLUDE),
    authoring = authoring.pass_thru("Copybara <copybara@example.com>"),
    mode = 'ITERATIVE',
    transformations = [
        remove_private_includes,
        remove_private_regions,
        core.move(".github/shared/pr_sync_shared.yml", WORKFLOW_FILE, overwrite=True),
        metadata.expose_label("GITHUB_PUBLIC_PR_NUMBER", new_name="Closes", separator=" #"),
        metadata.restore_author("ORIGINAL_AUTHOR", search_all_changes=True),
    ],
)


core.workflow(
    name = "pr",
    origin = git.github_pr_origin(
        url = SHARED_URL,
    ),
    destination = git.github_pr_destination(
        url = MAIN_URL,
        destination_ref = MAIN_BRANCH,
        pr_branch = "shared_${CONTEXT_REFERENCE}",
        title = "${GITHUB_PR_TITLE}",
        integrates = [],
    ),
    origin_files = glob(["**"], exclude=[WORKFLOW_FILE]),
    destination_files = glob(["**"], exclude=EXCLUDE),
    authoring = authoring.pass_thru("Copybara <copybara@example.com>"),
    mode = 'CHANGE_REQUEST',
    set_rev_id = False,
    transformations = [
        metadata.squash_notes("${GITHUB_PR_BODY}\n\nChanges:\n", compact=False, oldest_first=True, show_ref=False),
        metadata.expose_label("GITHUB_PR_NUMBER", new_name="GITHUB_PUBLIC_PR_NUMBER"),
        metadata.save_author("ORIGINAL_AUTHOR"),
    ],
)

